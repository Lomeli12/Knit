import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'fabric-loom' version '0.2.0-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
}

apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "Knit"
group = "net.lomeli"
version = "1.2.0"

minecraft {
}

repositories {
    maven { url "http://repo.elytradev.com/" }
    maven { url "http://maven.modmuss50.me/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${mcVersion}"
    mappings "net.fabricmc:yarn:${mappingVersion}"
    modCompile "net.fabricmc:fabric-loader:${loaderVersion}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile "net.fabricmc:fabric:${fabricVersion}"
    modCompile "io.github.prospector.modmenu:ModMenu:${modMenuVersion}"

    // So the javax annotations work.
    compileOnly "com.google.code.findbugs:jsr305:${findbugsVersion}"

    //jankson, the json parser we are using to allow comments
    shadow "blue.endless:jankson:${janksonVersion}"
    implementation "blue.endless:jankson:${janksonVersion}"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'fabric.mod.json'
        expand version: project.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'fabric.mod.json'
    }
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from sourceSets.main.resources
}

shadowJar {
    relocate 'blue.endless.jankson', 'net.lomeli.knit.repackage.blue.endless.jankson'
    classifier = "universal"
    configurations = [project.configurations.shadow]
}

remapJar {
    dependsOn shadowJar
    jar = shadowJar.archivePath
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = archivesBaseName
            version = "${mcVersion}-${version}"
            from components.java
            artifact tasks.shadowJar
            artifact tasks.sourcesJar
        }
    }
}

publishing {
    repositories {
        maven {
            url "https://maven.lomeli12.net/"
            credentials {
                username "${mavenUser}"
                password "${mavenPass}"
            }
        }
    }
}

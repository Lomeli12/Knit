import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'fabric-loom' version '0.2.0-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "Knit"
group = "net.lomeli.knit"
version = "1.0.0"

minecraft {
}

repositories {
    maven { url "http://repo.elytradev.com/" }
    maven { url "https://maven.lomeli12.net/"}
}

dependencies {
    minecraft "com.mojang:minecraft:${mcVersion}"
    mappings "net.fabricmc:yarn:${mappingVersion}"
    modCompile "net.fabricmc:fabric-loader:${loaderVersion}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile "net.fabricmc:fabric:${fabricVersion}"

    //jankson, the json parser we are using to allow comments
    shadow "blue.endless:jankson:${janksonVersion}"
    implementation "blue.endless:jankson:${janksonVersion}"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from sourceSets.main.resources
}

shadowJar {
    relocate 'blue.endless.jankson', 'net.lomeli.knit.repackage.blue.endless.jankson'
    classifier = "shadow"
    configurations = [project.configurations.shadow]
}

remapJar {
    dependsOn shadowJar
    jar = shadowJar.archivePath
}
